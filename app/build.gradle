/**
 * Repositories and dependencies for the build script
 */
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

/**
 * Repositories for the project
 * jCenter() is already called in project build.gradle
 */
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

/**
 * Apply plugins and other Gradle scripts
 */
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../checkstyle/checkstyle.gradle'

/**
 * Store keystore properties from keystore.properties in root folder if it exists
 */
def keystoreProperties = new Properties()
if (new File('keystore.properties').exists()) {
    def keystorePropertiesFile = rootProject.file('keystore.properties')
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties.keyAlias = 'your_key_alias'
    keystoreProperties.keyPassword = 'your_key_password'
    keystoreProperties.storeFile = 'your_keystore.jks'
    keystoreProperties.storePassword = 'your_keystore_password'
}

/**
 * Android build
 */
android {
    /**
     * Set configuration for signing release builds with keystore properties defined above
     */
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    /**
     * Android SDK and build-tools version
     * Update (both Gradle and local SDK) whenever possible
     */
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    /**
     * Build configurations for APK
     * Ensure the following values are updated before generating release builds:
     * versionCode (Increment of 1)
     * versionName (Based on Semantic Versioning 2.0 - http://semver.org/)
     */
    defaultConfig {
        minSdkVersion 19 /*Dont change this unless you know why*/
        targetSdkVersion 27 /*Dont change this unless you know why*/
        versionCode 39
        versionName "4.3.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    /**
     * Product flavors
     * Offline - No Internet permission and disable Crashlytics
     * Online - Internet permission and enable Crashlytics
     */
    flavorDimensions "default"

    productFlavors {
        offline {
            applicationId "com.swanberg.pogoiv.nointernet"
            buildConfigField "boolean", "isInternetAvailable", "false"
            ext.enableCrashlytics = false
            dimension "default"
        }

        online {
            applicationId "com.swanberg.pogoiv"
            buildConfigField "boolean", "isInternetAvailable", "true"
            ext.enableCrashlytics = true
            dimension "default"
        }
    }

    /**
     * Build type
     * debug - Disable Crashlytics
     * release - Minify and proguard files
     */
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
                checkReleaseBuilds false
            }
            signingConfig signingConfigs.releaseConfig
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a'/**, 'arm64-v8a', 'x86', 'x86_64'**/
        }
    }
    sourceSets {
        onlineRelease {
            // Contains the io.fabric.ApiKey meta-data
            manifest.srcFile 'src/onlineRelease/FabricManifest.xml'
        }
    }
}

/**
 * Project dependencies
 * compile - Compile library package into app APK
 * provided - Compile library package without included into app APK
 * apt - Compile library package without included into app APK (uses android-apt plugin for
 * annotation processing)
 */
dependencies {
    /**
     * Builds Dependencies for All Builds.
     */

    /**
     * Online Build Dependencies
     */
    //Include Crashlytics in the online build
    onlineCompile('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }

    /**
     * Test Build Dependencies
     */
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:preference-v7:27.0.2'
    compile 'com.android.support:recyclerview-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.google.guava:guava:23.5-android'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile 'com.jakewharton.timber:timber:4.3.0'
    compile 'com.rmtheis:tess-two:7.0.0'
    compile 'io.apptik.widget:multislider:1.2'
    compile 'com.pavelsikun:material-seekbar-preference:2.3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.android.support:support-annotations:27.0.2'
    testCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support:support-annotations:27.0.2'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    annotationProcessor 'org.projectlombok:lombok:1.16.18'
    onlineCompile 'com.squareup.okhttp3:okhttp:3.7.0'
    compileOnly 'org.projectlombok:lombok:1.16.18'
    compile project(':openCVLibrary330')
}

/**
 * Enable Java deprecation/unchecked warnings
 */
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// for importing custom build settings if following file exists.
def file = file('./custom.gradle')
if(file.exists()) apply from: file
